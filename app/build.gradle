apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 29

    buildToolsVersion '29.0.2'

    compileOptions.encoding = 'UTF-8'

    signingConfigs {
        release {
            storeFile file("$rootDir/diario.jks")
            storePassword "gdae5921"
            keyAlias "prodesp"
            keyPassword "gdae5921"
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        applicationId "br.gov.sp.educacao.sed.mobile"
        minSdkVersion 14
        targetSdkVersion 29
        vectorDrawables.useSupportLibrary = true
        versionCode 3200
        versionName '3.2.00'
        signingConfig signingConfigs.release
    }

    buildTypes {
        release {
            minifyEnabled false
            zipAlignEnabled true
            //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "ENABLE_MENU_TABLE", "false"
            aaptOptions {
                ignoreAssetsPattern '!*.json:!.DS_Store:'
            }
        }
        debug {
            minifyEnabled false
            buildConfigField "boolean", "ENABLE_MENU_TABLE", "true"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:2.0.0-beta4'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.google.android.gms:play-services-analytics:16.0.4'
    implementation 'com.google.firebase:firebase-analytics:16.0.5'
    implementation 'com.google.firebase:firebase-appindexing:17.1.0'
    implementation 'com.google.firebase:firebase-core:16.0.5'
    implementation 'com.google.firebase:firebase-messaging:17.3.4'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'com.roomorama:caldroid:2.1.0'
    implementation 'net.danlew:android.joda:2.9.9.1'
}

def signingProperties = "signing.properties"

def signingKeys = [
        storeFile    : { x -> file(x) },
        storePassword: { x -> x },
        keyAlias     : { x -> x },
        keyPassword  : { x -> x },
]

// Find signing.properties in the project root, or in $HOME/.gradle
def f = ["${rootDir}/${signingProperties}", "${gradle.gradleUserHomeDir}/${signingProperties}"].find {
    file(it).exists()
}

if (f) {
    logger.info "Loading signing properties from ${f}"
    def props = new Properties()
    props.load(new FileInputStream(f))

    // For each property apply it to the release signing config
    signingKeys.any { k, fn ->
        if (!props.containsKey(k)) {
            logger.error "Missing property ${k}"
            android.buildTypes.release.signingConfig = null
            return true
        }
        android.signingConfigs.release[k] = fn(props[k]);
        return
    }
} else {
    logger.info "Missing ${signingProperties} file"
    android.buildTypes.release.signingConfig = null
}

apply plugin: 'com.google.gms.google-services'